
include "macros.testo"


test server_install_ubuntu {
	server install_ubuntu("${server_hostname}", "${server_login}")
}

test server_prepare: server_install_ubuntu {
	server prepare_ubuntu("${server_hostname}", "${server_login}")
}

[no_snapshots: true]
test server_install_guest_additions: server_prepare {
	server install_guest_additions()
}

test server_setup_nic: server_install_guest_additions {
	server {
		copyto ".\\rename_net.sh" "/opt/rename_net.sh"
		exec bash """
			chmod +x /opt/rename_net.sh
			/opt/rename_net.sh 52:54:00:00:00:bb client_side
			ip a a 192.168.1.1/24 dev client_side
			ip l s client_side up
			ip ad
		"""
	}
}

test client_install_ubuntu {
	client install_ubuntu("${client_hostname}", "${client_login}", "1111")
}

test client_prepare: client_install_ubuntu {
	client prepare_ubuntu("${client_hostname}", "${client_login}", "1111")
}

[no_snapshots: true]
test client_install_guest_additions: client_prepare {
	client install_guest_additions()
}

test client_setup_nic: client_install_guest_additions {
	client {
		copyto ".\\rename_net.sh" "/opt/rename_net.sh"
		exec bash """
			chmod +x /opt/rename_net.sh
			/opt/rename_net.sh 52:54:00:00:00:aa server_side
			ip a a 192.168.1.2/24 dev server_side
			ip l s server_side up
			ip ad
		"""
	}
}

[no_snapshots: true]
test test_ping_1: client_setup_nic, server_setup_nic {
	client exec bash "ping 192.168.1.2 -c5"
}

[no_snapshots: true]
test test_ping_2: client_setup_nic, server_setup_nic {
	server exec bash "ping 192.168.1.1 -c5"
}

test install_ubuntu_desktop {
	ubuntu_desktop {
		start
		wait "Try Ubuntu without installing"; press Down, Enter
		wait "Welcome" timeout 5m
		mouse click "Continue".center_bottom()
		wait "Keyboard layout"
		mouse click "Continue".center_bottom()
		wait "Updates and other software"
		mouse click "Download updates while installing"; mouse click "Continue"
		wait "Installation type"; mouse move 0 0; mouse click "Install Now".center_bottom()
		wait "Write the changes to disks?"; mouse click "Continue".from_bottom(0).center_bottom()
		wait "Where are you?"; mouse click "Continue".center_bottom()
		wait "Who are you?";

		mouse click "Your name".right_center().move_right(20); type "${desktop_login}"
		mouse click "Your computer's name".right_center().move_right(20); press LeftCtrl + A, Delete;  type "${desktop_hostname}"
		mouse click "password:".from_top(0).right_center().move_right(20); type "${default_password}"
		mouse click "password:".from_top(1).right_center().move_right(20); type "${default_password}"
		mouse click "Continue".center_bottom()
		mouse click "Restart Now" timeout 30m
		wait "Please remove the installation medium" timeout 2m;
		unplug dvd; stop; start
		wait "${desktop_login}" && "Not listed?" timeout 3m

		mouse click "${desktop_login}";
		wait "Password"; type "${default_password}"; mouse click "Sign In"
		wait "Welcome to Ubuntu"
	}
}

test libre_office_launch: install_ubuntu_desktop {
	ubuntu_desktop {
		mouse click "Welcome to Ubuntu"
		mouse click "Quit"
		wait !"Welcome to Ubuntu"
		mouse click "Activities"
		wait "Type to search"; type "libre"
		mouse click "LibreOffice Calc"
		wait "Untitled 1 - LibreOffice Calc"
	}
}

test libre_office_demo: libre_office_launch {
	ubuntu_desktop {
		mouse rclick js """
			row_index = find_text('15').from_left(0).center()
			column_index = find_text('B').from_right(0).center()

			result = {x: column_index.x, y: row_index.y}
		"""

		wait "B15"
	}
}
