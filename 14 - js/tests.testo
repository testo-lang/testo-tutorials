include "macros.testo"

test server_install_ubuntu {
	server install_ubuntu("${server_hostname}", "${server_login}")
}

test server_install_guest_additions: server_install_ubuntu {
	server install_guest_additions("${server_hostname}", "${server_login}")
}

test server_unplug_nat: server_install_guest_additions {
	server unplug_nat("${server_hostname}", "${server_login}")
}

test server_prepare: server_unplug_nat {
	server {
		copyto "./rename_net.sh" "/opt/rename_net.sh"
		exec bash """
			chmod +x /opt/rename_net.sh
			/opt/rename_net.sh 52:54:00:00:00:bb client_side
			ip a a 192.168.1.1/24 dev client_side
			ip l s client_side up
			ip ad
		"""
	}
}

test client_install_ubuntu {
	client install_ubuntu("${client_hostname}", "${client_login}", "1111")
}

test client_install_guest_additions: client_install_ubuntu {
	client install_guest_additions("${client_hostname}", "${client_login}", "1111")
}

test client_unplug_nat: client_install_guest_additions {
	client unplug_nat("${client_hostname}", "${client_login}", "1111")
}

test client_prepare: client_unplug_nat {
	client {
		plug flash exchange_flash
		sleep 5s
		exec bash """
			mount /dev/sdb1 /media
			cp /media/rename_net.sh /opt/rename_net.sh
			umount /media
		"""
		unplug flash exchange_flash
		exec bash """
			chmod +x /opt/rename_net.sh
			/opt/rename_net.sh 52:54:00:00:00:aa server_side
			ip a a 192.168.1.2/24 dev server_side
			ip l s server_side up
			ip ad
		"""
	}
}

test test_ping: client_prepare, server_prepare {
	client exec bash "ping 192.168.1.2 -c5"
	server exec bash "ping 192.168.1.1 -c5"
}

#обратите внимание, тест отнаследован не от test_ping, 
#а тех же client_prepare и server_prepare
#То есть test_ping и exchange_files_with_flash лежат на одном уровне
#иерархии в дереве тестов
test exchange_files_with_flash: client_prepare, server_prepare {
	client {
		#Создаём файл, который нужно будет передать на сервер
		exec bash "echo \"Hello from client!\" > /tmp/copy_me_to_server.txt"

		plug flash exchange_flash
		sleep 5s
		exec bash """
			mount /dev/sdb1 /media
			cp /tmp/copy_me_to_server.txt /media
			umount /media
		"""

		unplug flash exchange_flash
	}

	server {
		plug flash exchange_flash
		sleep 5s
		exec bash """
			mount /dev/sdb1 /media
			cp /media/copy_me_to_server.txt /tmp
			umount /media
			cat /tmp/copy_me_to_server.txt
		"""

		unplug flash exchange_flash
	}
}

test install_ubuntu_desktop {
	ubuntu_desktop {
		start
		wait "English"
		press Enter
		wait "Try Ubuntu without installing"; press Down, Enter
		wait "Welcome" timeout 5m
		mouse click "Continue".center_bottom()
		wait "Keyboard layout"
		mouse click "Continue".center_bottom()
		wait "Updates and other software"
		mouse click "Download updates while installing"; mouse click "Continue"
		wait "Installation type"; mouse move 0 0; mouse click "Install Now".center_bottom()
		wait "Write the changes to disks?"; mouse click "Continue".from_bottom(0).center_bottom()
		wait "Where are you?"; mouse click "Continue".center_bottom()
		wait "Who are you?";

		mouse click "Your name".right_center().move_right(20); type "${desktop_login}"
		mouse click "Your computer's name".right_center().move_right(20); press LeftCtrl + A, Delete;  type "${desktop_hostname}"
		mouse click "password:".from_top(0).right_center().move_right(20); type "${default_password}"
		mouse click "password:".from_top(1).right_center().move_right(20); type "${default_password}"
		mouse click "Continue".center_bottom()
		mouse click "Restart Now" timeout 15m
		wait "Please remove the installation medium" timeout 2m;
		unplug dvd; stop; start
		wait "${desktop_login}" && "Not listed?" timeout 3m

		mouse click "${desktop_login}";
		wait "Password"; type "${default_password}"; mouse click "Sign In"
		wait "Welcome to Ubuntu"
	}
}

test libre_office_launch: install_ubuntu_desktop {
	ubuntu_desktop {
		mouse click "Welcome to Ubuntu"
		mouse click "Quit"
		wait !"Welcome to Ubuntu"
		mouse click "Activities"
		wait "Type to search"; type "libre"
		mouse click "LibreOffice Calc"
		wait "Untitled 1 - LibreOffice Calc"
	}
}

test libre_office_demo: libre_office_launch {
	ubuntu_desktop {
		mouse rclick js """
			row_index = find_text().match('15').from_left(0).center()
			column_index = find_text().match('G').from_right(0).center()

			result = {x: column_index.x, y: row_index.y}
		"""

		wait "G15"
	}
}